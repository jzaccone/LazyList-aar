buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android-library'
apply plugin: 'maven'

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.0'

    sourceSets {
        main {
            manifest.srcFile 'LazyList/AndroidManifest.xml'
            java.srcDirs = ['LazyList/src']
            res.srcDirs = ['LazyList/res']
        }
    }
}

dependencies {
	compile 'com.android.support:support-v4:+'
}

group = 'com.thest1.lazylist'
version = '1.0'
def artifactId = 'library'

def repositoryUrl
if (has('repoDir')) {
    repositoryUrl = "file://$repoDir"
} else {
    repositoryUrl = "file://$buildDir/repo/"
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: repositoryUrl)
                pom.artifactId = artifactId
            }
        }
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.allJava
    }
    task androidJavadocsJar(type: Jar) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.allSource
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}
